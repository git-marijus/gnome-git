# $Id: PKGBUILD 164253 2012-07-28 20:26:26Z eric $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwacom
pkgver=0.7
pkgrel=2
pkgdesc="Library to identify Wacom tablets and their features"
arch=('x86_64' 'i686')
url="http://sourceforge.net/apps/mediawiki/linuxwacom/index.php?title=Libwacom"
license=('MIT')
depends=('glib2' 'systemd-tools')
options=('!libtool')
source=(http://sourceforge.net/projects/linuxwacom/files/libwacom/$pkgname-$pkgver.tar.bz2)
sha256sums=('210c48b595461ebee63b1a846a6eb1d8f029b49d02ccbde34eadf551a5a68a78')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
