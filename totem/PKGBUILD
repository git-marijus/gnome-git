# $Id: PKGBUILD 173078 2012-12-10 11:38:58Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=20130101
pkgrel=1
pkgdesc="GNOME3 movie player based on GStreamer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(GPL2 custom)
depends=(gst-plugins-base gst-plugins-good totem-plparser desktop-file-utils iso-codes libpeas
         hicolor-icon-theme gnome-icon-theme-symbolic gsettings-desktop-schemas dconf clutter-gst
         clutter-gtk python2-gobject gst-plugins-bad)
makedepends=(intltool yelp-tools nautilus lirc-utils vala grilo pylint dbus-glib libzeitgeist gtk-doc gnome-common git)
options=('!libtool' '!emptydirs')
source=(browser-plugins.ini)
sha256sums=('a50a3bbf35f0535f7e8e20af1893446a2e5711015484f9ae6d1ff91af3b23c4e')

_gitroot=git://git.gnome.org/totem
_gitname=totem

build() {
    cd $srcdir

    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus
  make
}

package_totem() {
  groups=(gnome-extra)
  install=totem.install
  optdepends=('gst-plugins-ugly: Extra media codecs'
              'gst-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'grilo-plugins: Browse sidebar (remote media)'
              'pyxdg: opensubtitles plugin'
              'libzeitgeist: Zeitgeist plugin')

  cd $srcdir/$_gitname-build/
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/lib/mozilla"
  rm "$pkgdir/usr/lib/totem/totem-plugin-viewer"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/totem/totem-bugreport.py

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem/COPYING"
}

package_totem-plugin() {
  pkgdesc="Totem plugin for web browsers"
  depends=("totem=$pkgver" dbus-glib)
  backup=(etc/totem/browser-plugins.ini)

  cd $srcdir/$_gitname-build/
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="$pkgdir" install
  
  install -Dm644 ../browser-plugins.ini "$pkgdir/etc/totem/browser-plugins.ini"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem-plugin/COPYING"
}
