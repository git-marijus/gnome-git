# $Id: PKGBUILD 169233 2012-10-18 19:27:16Z jgc $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=20130101
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gsettings-desktop-schemas' 'gtk3' 'xkeyboard-config')
makedepends=(gnome-doc-utils intltool gobject-introspection itstool yelp-tools gtk-doc gnome-common git)
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool')

_gitroot=git://git.gnome.org/gnome-desktop
_gitname=gnome-desktop

build() {
    cd $srcdir

    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-desktop \
      --with-gnome-distributor="Arch Linux"
  make
}

package() {
  cd $srcdir/$_gitname-build/
  make DESTDIR="$pkgdir" install
}
