# Maintainer: marijus

pkgname=evince
pkgver=20130113
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://projects.gnome.org/evince/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libgxps' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre' 'gnome-icon-theme' 't1lib' 'libsecret' 'desktop-file-utils' 'dconf' 'gsettings-desktop-schemas')
makedepends=('itstool' 'nautilus' 'texlive-bin' 'intltool' 'gobject-introspection' 'yelp-tools' 'gtk-doc' 'gnome-common' 'git')
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')

_gitroot=git://git.gnome.org/evince
_gitname=evince

build() {
    cd $srcdir

    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."

  ./autogen.sh --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection
  make
}

package() {
  cd $srcdir/$_gitname-build/
  make DESTDIR="$pkgdir" install
}
